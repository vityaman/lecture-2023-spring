plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.7'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'org.openapi.generator' version '6.3.0'
}

group = 'ru.vityaman'
version = '0.0.1'
sourceCompatibility = '17'

sourceSets {
    main {
        java {
            srcDir("$rootDir/src/main/java")
            srcDir("$buildDir/generated/openapi/src/main/java")
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

springBoot {
    mainClass = 'ru.vityaman.demo.DemoApplication'
}

repositories {
	mavenCentral()
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.26'
	annotationProcessor 'org.projectlombok:lombok:1.18.26'

	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
	compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
	compileOnly 'javax.validation:validation-api:2.0.1.Final'

	implementation "org.openapitools:jackson-databind-nullable:0.2.4"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
	implementation "io.springfox:springfox-swagger2:2.10.5"
	implementation "org.springdoc:springdoc-openapi-ui:1.6.14"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testCompileOnly 'org.projectlombok:lombok:1.18.26'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
}

tasks.named('test') {
	useJUnitPlatform()
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/src/main/resources/static/api.yaml".toString()
    outputDir = "$buildDir/generated/openapi".toString()
    apiPackage = 'ru.vityaman.demo.api'
    modelPackage = 'ru.vityaman.demo.api.model'
	configOptions = [
			delegatePattern: "true",
			library: "spring-boot",
			dateLibrary : "java8",
			useTags: "true",
			unhandledException: "true",
			additionalModelTypeAnnotations: "@com.fasterxml.jackson.annotation.JsonInclude(com.fasterxml.jackson.annotation.JsonInclude.Include.NON_NULL)",
			useSwaggerUI: "false",
	]
}

compileJava.dependsOn tasks.openApiGenerate
